type id = string
type constructor = string [@@deriving to_yojson]
type loc [@@deriving to_yojson]

val pp_id : Format.formatter -> id -> unit
val pp_constructor : Format.formatter -> constructor -> unit
val pp_loc : Format.formatter -> loc -> unit
val string_of_id : id -> string
val string_of_constructor : constructor -> string
val string_of_loc : loc -> string
val fresh_loc : unit -> loc

type label = LocL of loc | ConsL of constructor | SymL of Symbolic.id [@@deriving to_yojson]

val fresh_evar : unit -> id

type pattern = PatCons of constructor * id | PatVar of id

type binary_op =
  | Plus
  | Minus
  | Mult
  | Div
  | And
  | Or
  | Equal
  | NEqual
  | Less
  | LessEq
  | Great
  | GreatEq

type unary_op = Not

type handler = Handler of (pattern * term)

and term =
  | Var of id
  | Constructor of constructor * term option
  | Name of Names.name
  | Loc of loc
  (* This constructor embeds a symbolic expression into a RefML expression *)
  | Symbolic of Symbolic.symbolic_expr
  | Unit
  | Int of int
  | Bool of bool
  | Record of (id, term) Util.Pmap.pmap
  | Projection of (term * id)
  | BinaryOp of binary_op * term * term
  | UnaryOp of unary_op * term
  | If of term * term * term
  | Fun of (id * Types.typ) * term
  | Fix of (id * Types.typ) * (id * Types.typ) * term
  | Let of id * term * term
  | LetPair of id * id * term * term
  | App of term * term
  | Seq of term * term
  | While of term * term
  | Pair of term * term
  | Newref of Types.typ * term
  | Deref of term
  | Assign of term * term
  | Assert of term
  | Raise of term
  | TryWith of (term * handler list)
  | Hole
  | Error

val pp_term : Format.formatter -> term -> unit
val string_of_term : term -> string

type name_set = Names.name list

val empty_name_set : name_set

(* get_new_name s t collects all the names appearing in the term t, and add them to s.
It guarantee that each new name is added only once in s, unless it was already in s in which case it is not added.*)
val get_new_names : name_set -> term -> name_set
val get_names : term -> name_set

type label_set = label list

val empty_label_set : label_set
val get_new_labels : label_set -> term -> label_set
val get_labels : term -> label_set

type value = term [@@deriving to_yojson]

val pp_value : Format.formatter -> value -> unit
val string_of_value : value -> string
val isval : term -> bool

(* The following function subst expr value value 'can be used to substitue any occurence of
   value by value' in expr. The second argument value can either be a variable, a Names.name, a location or the Hole.*)
val subst : term -> value -> value -> term
val subst_var : term -> id -> value -> term
val rename : term -> Renaming.Renaming.t -> term
val implement_arith_op : binary_op -> int -> int -> int
val implement_bin_bool_op : binary_op -> bool -> bool -> bool
val implement_compar_op : binary_op -> int -> int -> bool
val get_consfun_from_bin_cons : term -> term * term -> term
val get_consfun_from_un_cons : term -> term -> term

type val_env = (id, value) Util.Pmap.pmap [@@deriving to_yojson]

val string_of_val_env : val_env -> string
val empty_val_env : val_env

type eval_context [@@deriving to_yojson]

val pp_eval_context : Format.formatter -> eval_context -> unit
val string_of_eval_context : eval_context -> string
val empty_eval_context : eval_context
val rename_eval_context : Renaming.Renaming.t -> eval_context -> eval_context

type negative_val [@@deriving to_yojson]

val pp_negative_val : Format.formatter -> negative_val -> unit
val string_of_negative_val : negative_val -> string
val filter_negative_val : value -> negative_val option
val force_negative_val : value -> negative_val
val embed_negative_val : negative_val -> value
val rename_negative_val : Renaming.Renaming.t -> negative_val -> negative_val


val get_nf_term : term -> (value, eval_context, Names.name, unit) Nf.nf_term

val refold_nf_term :
  (value, unit, negative_val, eval_context) Nf.nf_term -> term

(* The following function should be replaced by generate_nup *)
val generate_ground_value : Types.typ -> value list
